import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        kotlin_version = '1.9.21'
        compose_version = '1.6.0-alpha04'
        compose_compiler_version = '1.5.3'
    }
    dependencies {
        classpath libs.hilt.android.gradle.plugin
        classpath libs.sqldelight.gradle.plugin
        classpath libs.google.services
    }

}// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.2.0-rc01' apply false
    id 'com.android.library' version '8.2.0-rc01' apply false
    id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false
    id 'app.cash.sqldelight' version '2.0.0' apply false
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin' version '2.0.1' apply false
    id "com.bnorm.power.kotlin-power-assert" version "0.13.0" apply false
    id "org.sonarqube" version "4.3.0.3225"
    id "com.autonomousapps.dependency-analysis" version "1.21.0" apply true
    id "dev.zacsweers.redacted" version "1.7.1" apply false
    id "com.google.devtools.ksp" version "1.9.10-1.0.13" apply false
    id 'com.mikepenz.aboutlibraries.plugin' version "10.8.3" apply false
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

sonarqube {
    properties {
        property "sonar.projectKey", "savvasdalkitsis_uhuruphotos-android"
        property "sonar.organization", "savvasdalkitsis"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

subprojects {
    if (name == "app") {
        apply plugin: "com.android.application"
    } else {
        apply plugin: "com.android.library"
    }

    apply plugin: "com.bnorm.power.kotlin-power-assert"
    apply plugin: 'org.jetbrains.kotlin.android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'dev.zacsweers.redacted'
    apply plugin: "com.google.devtools.ksp"

    android {
        compileSdk 34

        defaultConfig {
            minSdk 24
            targetSdk 34
        }
        buildTypes {
            release {
                minifyEnabled false
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }
        kotlinOptions {
            jvmTarget = '17'
        }
    }

    dependencies {
        kapt libs.hilt.dagger.compiler
        kapt libs.androidx.hilt.compiler
    }

    tasks.withType(KotlinCompile).configureEach {
        compilerOptions {
            languageVersion = KotlinVersion.KOTLIN_1_9
            freeCompilerArgs.addAll(
                    "-opt-in=androidx.compose.animation.ExperimentalAnimationApi",
                    "-opt-in=androidx.compose.animation.graphics.ExperimentalAnimationGraphicsApi",
                    "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi",
                    "-opt-in=androidx.compose.foundation.layout.ExperimentalLayoutApi",
                    "-opt-in=androidx.compose.material.ExperimentalMaterialApi",
                    "-opt-in=androidx.compose.material3.windowsizeclass.ExperimentalMaterial3WindowSizeClassApi",
                    "-opt-in=androidx.compose.ui.ExperimentalComposeUiApi",
                    "-opt-in=androidx.media3.common.util.UnstableApi",
                    "-opt-in=coil.annotation.ExperimentalCoilApi",
                    "-opt-in=com.google.accompanist.permissions.ExperimentalPermissionsApi",
                    "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                    "-opt-in=kotlinx.coroutines.FlowPreview",
                    "-opt-in=kotlinx.ExperimentalStdlibApi",
                    "-opt-in=kotlin.ExperimentalStdlibApi",
                    "-Xcontext-receivers",
            )
        }
    }
}