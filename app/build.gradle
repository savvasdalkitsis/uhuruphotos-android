plugins {
    id 'dagger.hilt.android.plugin'
    alias(libs.plugins.google.services)
    alias(libs.plugins.about)
    id 'com.bugsnag.android.gradle'
}

android {
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.compiler.get()
    }

    defaultConfig {
        applicationId "com.savvasdalkitsis.uhuruphotos"
        minSdk 24
        targetSdk 34
        versionCode 220
        versionName "0.0.220"

        manifestPlaceholders = [
                bugSnagApiKey: System.getenv("BUGSNAG_API_KEY") ?: "DUMMY-KEY",
        ]

        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file("keystore/keystore.jks")
            storePassword System.getenv("KEYSTORE_PASS")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASS")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            proguardFiles fileTree(dir: "proguards", include: ["*.pro"]).asList().toArray()

            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/AL2.0'
            excludes += '/META-INF/LGPL2.1'
            excludes += '/META-INF/versions/*'
        }
    }
    namespace 'com.savvasdalkitsis.uhuruphotos'
}

dependencies {
    implementation(
            libs.accompanist.systemui,
            libs.androidx.activity,
            libs.androidx.compose.hilt.navigation,
            libs.androidx.compose.material.windowsize,
            libs.androidx.compose.ui,
            libs.androidx.splash,
            libs.androidx.work.hilt,
            libs.bundles.autodagger,
            libs.bugsnag,
            libs.hilt,
            libs.permissions.base,
            project(":feature:auth:domain:api"),
            project(":feature:auth:view:api"),
            project(":feature:download:domain:api"),
            project(":feature:home:view:api"),
            project(":feature:settings:domain:api"),
            project(":foundation:activity:api"),
            project(":foundation:image:api"),
            project(":foundation:initializer:api"),
            project(":foundation:log:api"),
            project(":foundation:map:api"),
            project(":foundation:navigation:api"),
            project(":foundation:ui:api"),
            project(":foundation:ui:implementation"),
            project(":foundation:video:api"),
            project(":app:implementation"),
    )
    debugImplementation(
            libs.leaks,
    )
}